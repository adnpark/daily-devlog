# 1. Fallback

## Problem
Look carefully at the contract's code below.

You will beat this level if

1. you claim ownership of the contract
2. you reduce its balance to 0

Things that might help
- How to send ether when interacting with an ABI
- How to send ether outside of the ABI
- Converting to and from wei/ether units (see help() command)
- Fallback methods

## Solution
먼저 컨트랙트의 ownership을 가져온 뒤, 컨트랙트의 balance를 0으로 만들면 되는 게임이다.

먼저 이 컨트랙트의 오너가 누구인지 확인하자

0x9CB391dbcD447E645D6Cb55dE6ca23164130D008 -> Contract owner
0xe8366E50ed089Eea4df663116e257B5D79fbD953 -> My address

당연하게도 아직 내 어카운트가 owner가 아니다.

먼저 가장 단순하게 컨트랙트의 owner가 되는 방법은 contribute() 함수를 이용하는 것이다.
contribute() 함수는 컨트랙트에 ETH를 전송할 수 있게 하고, 전송한 Ether만큼 해당 어카운트의 contributions수치를 증가시킨다.
그리고 만약 contributions가 기존 owner의 그것보다 더 커지게 되면 ownership을 가져올 수 있게 된다.

그런데 한가지 문제가 있다. owner의 contribution은 무려 1000 ether로 설정되어 있다.
여기서 더 심각한것은 contribute() 함수를 통해 컨트랙트에 전송할 수 있는 이더의 단위는 0.001 ether 인 것이다.
때문에 1000 ether를 어찌저찌 마련한다쳐도 contribute() 함수를 약 1백만번이 넘게 호출해야 하는 불상사가 생긴다.
트랜잭션 수수료를 감안하면 실제로 필요한 금액은 1000 ether를 훨씬 상회할 것이다.

따라서 우리는 다른 해결방법을 찾아내야 한다.

여기서 주목할 함수가 바로 receive() 이다.
receive() 함수가 무엇인지 이해하기 위해서는 fallback() 함수를 이해해야 한다.

fallback 함수란 만일을 위한 대비책 함수를 의미한다. 
0.6.0 버전 이후로 솔리디티에는 두가지 fallback 함수가 존재한다.

1. receive()
2. fallback()

receive() 함수는 어떠한 calldata 없이 오로지 컨트랙트에 이더만을 전송할 때 호출된다. 

fallback() 함수는 호출하고자 하는 함수와 매칭되는 함수가 컨트랙트에 존재하지 않을 때 호출된다. 물론 동시에 이더도 받을 수 있다.

calldata가 무엇인지 모르겠다면, [이 글](https://medium.com/@aiden.p/solidity-%EC%94%A8-%EB%A6%AC%EC%A6%88-%EC%9D%80%EA%B7%BC-%ED%97%B7%EA%B0%88%EB%A6%AC%EB%8A%94-data-location-2690cefb72db)의 설명을 참고해보자.

fallback 함수에 대해 더 자세히 알고싶다면 이 [문서](https://blog.soliditylang.org/2020/03/26/fallback-receive-split/)를 참고해보자.

다시 돌아가서 receive() 함수가 어떻게 구현되어 있는지 보자.
컨트랙트에 0보다 큰 ether를 전송하고, 동시에 contributions가 0보다 큰 경우 ownership을 msg.sender에게 이전한다!

즉, 우리의 과제는 이 조건들을 만족시키는 것이다.

contributions를 높이기 위해서는 contribute() 함수를 사용하면 된다. 이 때 전송할 ether 값인 msg.value는 0 < msg.value < 0.001 ether 만 만족하면 된다.

컨트랙트의 함수를 호출하면서 이더를 전송하고 싶다면 아래와 같이 콘솔에 입력하면 된다.

`await contract.contribute({value: 1})`

우리는 이를 통해 최소값인 1 wei만을 컨트랙트에 전송하고, contributions의 값을 1로 설정할 수 있게 되었다.

`await contract.getContribution()` 을 실행하여 내 contributions의 값이 1로 변경되었는지 확인해보자.

이제 contributions > 0 이라는 조건을 만족했으므로, receive() 를 호출할 차례다.

컨트랙트 함수를 호출하지 않고 오직 이더만 전송하기 위해서는 아래와 같이 입력하면 된다.

`await contract.send(1)`

트랜잭션이 성공적으로 실행되었다면 이제 contract의 owner가 잘 변경되었는지 확인해보자.

`await contract.owner()` 의 return 값이 본인의 어카운트 주소면 성공이다.

이제 ownership은 성공적으로 가져왔고, 남은 2단계인 컨트랙트의 balance를 0으로 만들 차례이다.

여기까지 잘 따라왔다면 컨트랙트의 balance는 2 wei가 되어있을 것이다. 바로 확인해보자. 

`await getBalance(contract.address)`

0.000000000000000002 값이 리턴될 것이다. 조금 더 보기 좋게 wei 단위로 바꿔보자.

`toWei(await getBalance(contract.address))`

2 값을 확인할 수 있다.

컨트랙트의 withdraw() 함수를 이용하면 컨트랙트의 모든 balance를 출금할 수 있다. 아래 명령어를 실행해보자.

`await contract.withdraw()`

다시 컨트랙트의 balance를 확인해보자.

`await getBalance(contract.address)`

return 값이 0이면 성공이다!

## Review
이로써 1. Fallback 스테이지를 클리어했다. 이 스테이지에서 우리가 얻어가야 할 것은 fallback 함수에 대한 이해이다. `receive(), fallback()` 함수들을 제대로 이해하고, 어떻게 사용하는지 잘 숙지하자. 또한 더불어 컨트랙트에 ether를 전송하는 다양한 경우를 실습해봤다. 이 또한 실제 개발에서 흔히 쓰이게 되니 잘 숙지해두도록 하자.

## Sources
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';

contract Fallback {

  using SafeMath for uint256;
  mapping(address => uint) public contributions;
  address payable public owner;

  constructor() public {
    owner = msg.sender;
    contributions[msg.sender] = 1000 * (1 ether);
  }

  modifier onlyOwner {
        require(
            msg.sender == owner,
            "caller is not the owner"
        );
        _;
    }

  function contribute() public payable {
    require(msg.value < 0.001 ether);
    contributions[msg.sender] += msg.value;
    if(contributions[msg.sender] > contributions[owner]) {
      owner = msg.sender;
    }
  }

  function getContribution() public view returns (uint) {
    return contributions[msg.sender];
  }

  function withdraw() public onlyOwner {
    owner.transfer(address(this).balance);
  }

  receive() external payable {
    require(msg.value > 0 && contributions[msg.sender] > 0);
    owner = msg.sender;
  }
}
```