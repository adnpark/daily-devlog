# 1. Fallback

## Problem
Claim ownership of the contract below to complete this level.

Things that might help
- Solidity Remix IDE

## Solution
이 문제는 사실 최근에 솔리디티 개발을 접했다면 오히려 쉽게 풀 수 있는 문제이다. 

Fal1out() 함수 내부에서 owner를 msg.sender로 변경하고 있다. 따라서 Fal1out() 함수를 호출하면 해결된다.

문제는 주석에도 친절하게 constructor라고 되어 있듯이, 이 함수가 컨트랙트 배포시에 단 한번만 실행되는 생성자 함수인것처럼 보인다는 것이다.

실제로 기존에는 컨트랙트와 동일한 이름을 가진 함수(이 경우에는 Fallout())가 constructor로 사용되었다. 하지만 이는 개발자에게 혼란을 줄 수 있다는 이유로 0.4.23 버전 이후로 명시적으로 constructor() 라는 이름으로 생성자 함수를 선언하도록 변경되었다.

때문에 Fallout 컨트랙트를 컴파일하면 아래와 같은 에러가 발생하는것을 확인할 수 있다.

`contracts/Fallout.sol:11:3: SyntaxError: Functions are not allowed to have the same name as the contract. If you intend this to be a constructor, use "constructor(...) { ... }" to define it.`

에러 메세지를 요악하면 컨트랙트와 동일한 이름의 함수는 더 이상 허용되지 않는다는 것이다. 대신 constructor 문법을 사용하라고 권고하고 있다.

그런데 말이다. 만약 이 문제의 코드를 그대로 Remix IDE로 가져가서 컴파일해보면 그대로 컴파일이 성공하는걸 확인할 수 있다!

*Remix에서 컴파일을 해보면 아마도 라이브러리 관련 충돌이 발생할텐데, `import '@openzeppelin/contracts/math/SafeMath.sol';` 부분은 삭제하고, `allocations[msg.sender] = allocations[msg.sender].add(msg.value);` 는 `allocations[msg.sender] = allocations[msg.sender] + msg.value;`로 변경하자.*

그 이유는 자세히 보면 Fallout()이 아니라 `l` 이 `1`로 바뀐 Fal1out() 임을 알 수 있다. 즉, Fal1out() 함수는 constructor가 아니라 그냥 일반 함수인 것이다. 
따라서 Fal1out() 함수를 호출하는것으로 이번 스테이지를 클리어할 수 있게 된다.


## Review
이 스테이지의 의도는 constructor 함수명으로 인해 발생할 수 있는 사소하지만 중대한 보안 이슈를 상기시키기 위함으로 보인다. 하지만, 상기한대로 0.4.23 버전 이후로 생성자 함수는 constructor()를 사용하는것으로 변경되었기 때문에, 이 스테이지에서 상기시키고자 했던 이슈는 발생할 가능성이 없다고 할 수 있다.

그럼에도 불구하고 아주 사소한 실수로 인해 심각한 보안 이슈가 발생할 수 있다는점은 좋은 시사점이다.

## Sources
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';

contract Fallout {
  
  using SafeMath for uint256;
  mapping (address => uint) allocations;
  address payable public owner;


  /* constructor */
  function Fal1out() public payable {
    owner = msg.sender;
    allocations[owner] = msg.value;
  }

  modifier onlyOwner {
	        require(
	            msg.sender == owner,
	            "caller is not the owner"
	        );
	        _;
	    }

  function allocate() public payable {
    allocations[msg.sender] = allocations[msg.sender].add(msg.value);
  }

  function sendAllocation(address payable allocator) public {
    require(allocations[allocator] > 0);
    allocator.transfer(allocations[allocator]);
  }

  function collectAllocations() public onlyOwner {
    msg.sender.transfer(address(this).balance);
  }

  function allocatorBalance(address allocator) public view returns (uint) {
    return allocations[allocator];
  }
}
```